import br.pedroso.tweetsentiment.buildsrc.AndroidConfig
import br.pedroso.tweetsentiment.buildsrc.Dependencies

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion AndroidConfig.compileSdk
    defaultConfig {
        applicationId AndroidConfig.applicationId
        minSdkVersion AndroidConfig.minSdk
        targetSdkVersion AndroidConfig.targetSdk
        versionCode AndroidConfig.versionCode
        versionName AndroidConfig.versionName
        testInstrumentationRunner AndroidConfig.testInstrumentationRunner

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }

    buildTypes.each {
        it.buildConfigField 'String', 'NATURAL_LANGUAGE_API_KEY', NaturalLanguageApiKey
        it.buildConfigField 'String', 'TWITTER_CONSUMER_KEY', TwitterConsumerKey
        it.buildConfigField 'String', 'TWITTER_CONSUMER_SECRET', TwitterConsumerSecret
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':device')
    implementation project(':network')
    implementation project(':domain')
    implementation project(':presentation')


    Dependencies.appDependencies.each { implementation it }
    Dependencies.testDependencies.each { testImplementation it }
    Dependencies.androidTestDependencies.each { androidTestImplementation it }
}
